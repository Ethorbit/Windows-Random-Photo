/* TIScript code by: Ethan Christie */

//////////////////////////////////////////////////////////////
// Handle button menus:
function ExtendMenu()
{
    for (var v in $$(.BtnMenu))
    { 
        v.style["height"] = 5000;
        v.style["height"] = GetTallestElement(v.$(.container)).box(#height) + $(#ImgOverlay).box(#height);
        v.$(.container).style["height"] = v.box(#height);
    }
}

function HideMenus(whitelist)
{
    for (var v in $$(.BtnMenu)) // Hide all other menus
    {
        v.style["height"] = 0;
        if (v != whitelist) v.style["visibility"] = "none";
    }
}

function ShowMenu(element)
{
    HideMenus(element);
    element.style["visibility"] = "visible";
    element.attributes["fade"] = "none";
    element.attributes["fade"] = "in";
    if (element == $(#SettingsMenu))
        TryDefaultSettings();

    ExtendMenu();
    HideArrows();
}

//////////////////////////////////////////////////////////////
// Add image menu functionality:
var onImgMenuBtn = false;
for (var (k,v) in $$(.ImgMenuBtn))
{
    v.on("mouseenter", () => 
    {
        onImgMenuBtn = true;
    })

    v.on("mouseleave", () => 
    {
        onImgMenuBtn = false;
    })
}

$(#ImgOverlay).on("mousemove", () =>
{  
    MovedMouse = true;
    ShowContentOverlay();
});

$(#ImgOverlay).on("mouseleave", () =>
{
    HideContentOverlay();
});

event mousestop
{
    MovedMouse = false;

    var savedTime = GetSetting("OverlayHideTime").toString() + "ms";

    self.timer(Duration.parse(savedTime), () =>
    {
        if (!MovedMouse)
            HideContentOverlay();
        return false;
    });
}

function ShowContentOverlay()
{
    if (!onToolbar && !MenuIsOpen())
    {
        $(#ImgMenu).style["visibility"] = "visible";  

        if (GetSetting("OverlayHideToolbar") == true)
        {
            $(#toolbar).style["visibility"] = "visible";
        }
    } 
}

function HideContentOverlay()
{
    if (!onImgMenuBtn)
    {
        if (GetSetting("OverlayHideToolbar") == true)
        {
            $(#toolbar).style["visibility"] = "hidden";
        }

        $(#ImgMenu).style["visibility"] = "none";
    }
}

///////////////////////////////////////////////////////////////////////////
// Handle custom element tool tips:
for (var (k, v) in $$(img[alt=hint]))
{
    var eleCount = k;
    v.on("mouseenter", (me) =>
    {   
        var hintTxt = $$(.HintText)[eleCount];
        hintTxt.style["visibility"] = "visible";
        var (xx, yy) = view.cursorLocation();
        hintTxt.style["top"] = yy;
        hintTxt.style["left"] = xx - 50;
    });

    v.on("mouseleave", (me) =>
    {   
        $$(.HintText)[eleCount].style["visibility"] = "none";
    });
}
////////////////////////////////////////////////////////////
// Handle slider displays:
function UpdateSlider(slider)
{
    var nextEle = slider.next;

    if (nextEle.attributes["data"] == "s")
        nextEle.text = slider.value + "s";
    if (nextEle.attributes["data"] == "ms")
        nextEle.text = slider.value + "ms";
    if (nextEle.attributes["data"] == "%")  
        nextEle.text = slider.value + "%";
}

for (var v in $$(.sliderEle))
{
    v.on("change", (me) =>
    {
        UpdateSlider(me.target);
    });
}

//////////////////////////////////////////////////////////////
// Handle hotkey input boxes:
function DuplicateKeyMsg()
{   
    var element = $(#SettingsMenu).$(#DuplicateError);
    element.style["visibility"] = "visible";

    var bContinue = true;
    self.timer(1ms, () =>
    {
        if (bContinue)
        {
            element.style["top"] = centerHeight + $(#ImgOverlay).box(#height) - 70;
            return true;
        }
        else
            return false;
    });

    self.timer(1000ms, () =>
    {
        element.style["visibility"] = "none";
        bContinue = false;
        return false;
    });
}

function NoDuplicateKeys()
{
    for (var a in $(#SettingsMenu).$$(input[type=text]))
    {
        for (var b in $(#SettingsMenu).$$(input[type=text]))
        {
            if (a.text == b.text && a != b && a.text != "Enter a key") 
            {
                NoKeyName(b);
                DuplicateKeyMsg();

                if (view.getINI("Hotkeys", b.attributes["name"]) == a.text)
                    view.saveINI("Hotkeys", b.attributes["name"], null);
            }
        }
    }
}

for (var v in $(#SettingsMenu).$$(input[type=text]))
{
    v.subscribe("keydown", (me) =>
    {
        for (var (keyname, keycode) in Event)
        {
            var keyStr = keyname.toString();
            if (keyStr.substr(0, 2) == "VK")
            {
                if (keycode == me.keyCode)
                {
                    var hotkey = keyStr.replace("VK_", "");
                    me.target.attributes["maxlength"] = hotkey.length;
                    me.target.text = hotkey;
                    NoDuplicateKeys();
                }
            }
        }
    });
}

////////////////////////////////////////////////////////////////
// Default Settings:
function TryDefaultSettings()
{
    function SetValue(element, value)
    {
        if (element.attributes["type"] == "text")
        {
            element.attributes["maxlength"] = value.length;
            element.text = value;
        }
        else
        {
            if (element.attributes["type"] == "checkbox")
            {
                if (value == true)
                    element.checked = true;
            }
            else
            {
                element.value = value;

                if (element.attributes["type"] == "hslider")
                    UpdateSlider(element);
            }
        }
    }

    var mName = "";
    for (var v in $(#SettingsMenu).$$(input))
    {
        mName = v.attributes["name"];
        var mSetting = GetSettingObject(mName);
        var defSetting = GetDefSettingObject(mName);
        
        var savedKey = "";
        savedKey = view.getINI(mSetting.section, mName);        

        if (SettingIsSaved(mSetting.section, mName))
            SetValue(v, savedKey);     
        else if (IsADefaultSetting(mName))
            SetValue(v, defSetting.value);
        else 
        {
            if (v.attributes["type"] == "text")
                NoKeyName(v);
        }
    }
}